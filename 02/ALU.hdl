// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS: // this was difficult couldn't do now, will try later
      Mux16(a=x, b=false, sel=zx, out=t1);
        Mux16(a=y, b=false, sel=zy, out=t2);
        Not16(in=t1, out=notT1);
        Not16(in=t2, out=notT2);
        Mux16(a=t1, b=notT1, sel=nx, out=t3);
        Mux16(a=t2, b=notT2, sel=ny, out=t4);
        Add16(a=t3, b=t4, out=sumT3T4);
        And16(a=t3, b=t4, out=andT3T4);
        Mux16(a=andT3T4, b=sumT3T4, sel=f, out=t5);
        Not16(in=t5, out=notT5);
        Mux16(a=t5, b=notT5, sel=no, out=out, out[15]=msbOut, out[0..7]=rightOut, out[8..15]=leftOut);
        And(a=msbOut, b=true, out=ng);
        Or8Way(in=leftOut, out=orWayLeftOut);
        Or8Way(in=rightOut, out=orWayRightOut);
        Or(a=orWayLeftOut, b=orWayRightOut, out=orWayOut);
        Not(in=orWayOut, out=zr);

}